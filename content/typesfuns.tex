\section{Types and Functions}

\subsection{Primitive Types}

\Idris{} defines several primitive types: \tTC{Int}, \tTC{Integer} and \tTC{Float} for numeric operations, \tTC{Char} and \tTC{String} for text manipulation, and \tTC{Ptr} which represents foreign pointers.
There are also several data types declared in the library, including \tTC{Bool}, with values \tDC{True} and \tDC{False}.
We can declare some constants with these types.
Enter the following into a file \texttt{prims.idr} and load it into the \Idris{} interactive environment by typing \texttt{idris prims.idr}:

\inputIdrisListing{./examples/prims.idr}

\noindent
An \Idris{} file consists of an optional module declaration (here \texttt{module prims}) followed by an optional list of imports (none here, however \Idris{} programs can consist of several modules, and the definitions in each module each have their own namespace, as we will discuss in Section~\ref{sect:namespaces}) and a collection of declarations and definitions.
The order of definitions is significant --- functions and data types must be defined before use.
Each definition must have a type declaration, for example see \texttt{x : Int}, \texttt{foo : String}, from the above listing.
Indentation is significant --- a new declaration begins at the same level of indentation as the preceding declaration.
Alternatively, declarations may be terminated with a semicolon.

A library module \texttt{prelude} is automatically imported by every \Idris{} program, including facilities for IO, arithmetic, data structures and various common functions.
The prelude defines several arithmetic and comparison operators, which we can use at the prompt. Evaluating things at the prompt gives an answer, and the type of the answer.
For example:

\begin{lstlisting}[style=stdout]
*prims> 6*6+6
42 : Int
*prims> x == 6*6+6
True : Bool
\end{lstlisting}

\noindent
All of the usual arithmetic and comparison operators are defined for the primitive types.
They are overloaded using type classes, as we will discuss in Section~\ref{sec:classes} and can be extended to work on user defined types.
Boolean expressions can be tested with the \texttt{if...then...else} construct:

\begin{lstlisting}[style=stdout]
*prims> if x == 6 * 6 + 6 then "The answer!" else "Not the answer"
"The answer!" : String
\end{lstlisting}

\subsection{Data Types}

Data types are declared in a similar way to Haskell data types, with a similar syntax.
Natural numbers and lists, for example, can be declared as follows:

\begin{code}
data Nat    = Z   | S Nat           -- Natural numbers
                                    -- (zero and successor)
data List a = Nil | (::) a (List a) -- Polymorphic lists
\end{code}

\noindent
The above declarations are taken from the standard library.
Unary natural numbers can be either zero (\texttt{Z}), or the successor of another natural number (\texttt{S k}).
Lists can either be empty (\texttt{Nil}) or a value added to the front of another list (\texttt{x :: xs}).
In the declaration for \tTC{List}, we used an infix operator \tDC{::}.
New operators such as this can be added using a fixity declaration, as follows:

\begin{code}
infixr 10 ::
\end{code}

\noindent
Functions, data constructors and type constructors may all be given infix operators as names.
They may be used in prefix form if enclosed in brackets, e.g.\ \tDC{(::)}.
Infix operators can use any of the symbols:

\begin{lstlisting}[style=stdout]
:+-*/=_.?|&><!@$%^~.
\end{lstlisting}

\subsection{Functions}

Functions are implemented by pattern matching, again using a similar syntax to Haskell.
The main difference is that \Idris{} requires type declarations for all functions, using a single colon \texttt{:} (rather than Haskell's double colon \texttt{::}).
Some natural number arithmetic functions can be defined as follows, again taken from the standard library:

\begin{code}
-- Unary addition
plus : Nat -> Nat -> Nat
plus Z     y = y
plus (S k) y = S (plus k y)

-- Unary multiplication
mult : Nat -> Nat -> Nat
mult Z     y = Z
mult (S k) y = plus y (mult k y)
\end{code}

\noindent
The standard arithmetic operators \texttt{+} and \texttt{*} are also overloaded for use by \texttt{Nat}, and are implemented using the above functions.
Unlike Haskell, there is no restriction on whether types and function names must begin with a capital letter or not.
Function names (\tFN{plus} and \tFN{mult} above), data constructors (\tDC{Z}, \tDC{S}, \tDC{Nil} and \tDC{::}) and type constructors (\tTC{Nat} and \tTC{List}) are all part of the same namespace.
We can test these functions at the \Idris{} prompt:

\begin{lstlisting}[style=stdout]
Idris> plus (S (S Z)) (S (S Z))
4 : Nat
Idris> mult (S (S (S Z))) (plus (S (S Z)) (S (S Z)))
12 : Nat
\end{lstlisting}

\noindent
\textbf{Note:} \Idris{} automatically sugars the \texttt{Nat} representation into a more human readable format. The result of \verb!plus (S (S Z)) (S (S Z))! is actually \verb!(S (S (S (S Z))))! which is the Integer 4. This can be checked at the \Idris{} prompt:

\begin{lstlisting}[style=stdout]
Idris> (S (S (S (S Z))))
4 : Nat
\end{lstlisting}

\noindent
Like arithmetic operations, integer literals are also overloaded using type classes, meaning that we can also test the functions as follows:

\begin{lstlisting}[style=stdout]
Idris> plus 2 2
4 : Nat
Idris> mult 3 (plus 2 2)
12 : Nat
\end{lstlisting}

\noindent
You may wonder, by the way, why we have unary natural numbers when our computers have perfectly good integer arithmetic built in.
The reason is primarily that unary numbers have a very convenient structure which is easy to reason about, and easy to relate to other data structures as we will see later.
Nevertheless, we do not want this convenience to be at the expense of efficiency.
Fortunately, \Idris{} knows about the relationship between \tTC{Nat} (and similarly structured types) and numbers. This means it can optimise the representation, and functions such as \tFN{plus} and \tFN{mult}.

\subsubsection*{\texttt{where} clauses}

Functions can also be defined \emph{locally} using \texttt{where} clauses.
For example, to define a function which reverses a list, we can use an auxiliary function which accumulates the new, reversed list, and which does not need to be visible globally:

\begin{code}
reverse : List a -> List a
reverse xs = revAcc [] xs where
  revAcc : List a -> List a -> List a
  revAcc acc [] = acc
  revAcc acc (x :: xs) = revAcc (x :: acc) xs
\end{code}

\noindent
Indentation is significant --- functions in the \texttt{where} block must be indented further than the outer function.

\textbf{Scope:}
Any names which are visible in the outer scope are also visible in the \texttt{where} clause (unless they have been redefined, such as \texttt{xs} here).
A name which appears only in the type will be in scope in the \texttt{where} clause if it is a \emph{parameter} to one of the types, i.e.\ it is fixed across the entire structure.

As well as functions, \texttt{where} blocks can include local data declarations, such as the following where \texttt{MyLT} is not accessible outside the definition of \texttt{foo}:

\begin{code}
foo : Int -> Int
foo x = case isLT of
            Yes => x*2
            No => x*4
    where
       data MyLT = Yes | No

       isLT : MyLT
       isLT = if x < 20 then Yes else No
\end{code}

\noindent
In general, functions defined in a \texttt{where} clause need a type declaration just like any top level function.
However, the type declaration for a function \texttt{f} \emph{can} be omitted if:

\begin{itemize}
\item \texttt{f} appears in the right hand side of the top level definition
\item The type of \texttt{f} can be completely determined from its first application
\end{itemize}

\noindent
So, for example, the following definitions are legal:

\inputIdrisListing[firstline=3]{./examples/wheres.idr}

\subsection{Dependent Types}

\subsubsection{Vectors}

A standard example of a dependent type is the type of ``lists with length'',
conventionally called vectors in the dependent type literature.  
They are available as part of the \Idris{} library, by importing
\texttt{Data.Vect}, or we can declare them as follows:

\begin{code}
data Vect : Nat -> Type -> Type where
   Nil  : Vect Z a
   (::) : a -> Vect k a -> Vect (S k) a
\end{code}

\noindent
Note that we have used the same constructor names as for \tTC{List}.
Ad-hoc name overloading such as this is accepted by \Idris{}, provided that the names are declared in different namespaces (in practice, normally in different modules).
Ambiguous constructor names can normally be resolved from context.

This declares a family of types, and so the form of the declaration is rather
different from the simple type declarations above.  We explicitly state the
type of the type constructor \tTC{Vect} --- it takes a \tTC{Nat} and a type as
an argument, where \tTC{Type} stands for the type of types.  We say that
\tTC{Vect} is \emph{indexed} over \tTC{Nat} and \emph{parameterised} by
\tTC{Type}.  Each constructor targets a different part of the family of types.
\tDC{Nil} can only be used to construct vectors with zero length, and \tDC{::}
to construct vectors with non-zero length.  In the type of \tDC{::}, we state
explicitly that an element of type \texttt{a} and a tail of type \texttt{Vect k
a} (i.e., a vector of length \texttt{k}) combine to make a vector of length
\texttt{S k}.

We can define functions on dependent types such as \tTC{Vect} in the same way as on simple types such as \tTC{List} and \tTC{Nat} above, by pattern matching.
The type of a function over \tTC{Vect} will describe what happens to the lengths of the vectors involved.
For example, \tFN{++}, defined as follows, appends two \tTC{Vect}s:

\begin{code}
(++) : Vect n a -> Vect m a -> Vect (n + m) a
(++) Nil       ys = ys
(++) (x :: xs) ys = x :: xs ++ ys
\end{code}

\noindent
The type of \tFN{(++)} states that the resulting vector's length will be the sum of the input lengths.
If we get the definition wrong in such a way that this does not hold, \Idris{} will not accept the definition.
For example:

\begin{code}
(++) : Vect n a -> Vect m a -> Vect (n + m) a
(++) Nil       ys = ys
(++) (x :: xs) ys = x :: xs ++ xs -- BROKEN
\end{code}

\noindent
When run through the \Idris{} type checker, this results in the following:

\begin{lstlisting}[style=stdout]
$ idris vbroken.idr --check
vbroken.idr:9:23:When elaborating right hand side of Vect.++:
When elaborating an application of constructor Vect.:::
        Can't unify
                Vect (k + k) a
        with
                Vect (plus k m) a
        
        Specifically:
                Can't unify
                        plus k k
                with
                        plus k m
\end{lstlisting}

\noindent
This error message suggests that there is a length mismatch between two vectors
--- we needed a vector of length \texttt{k + m}, but provided a vector of length
\texttt{k + k}.

% Note that the terms in the error message have been \emph{normalised}, so in particular \texttt{n + m} has been reduced to \texttt{plus n m}.

\subsubsection{The Finite Sets}

Finite sets, as the name suggests, are sets with a finite number of elements.
They are available as part of the \Idris{} library, by importing
\texttt{Data.Fin}, or can be declared as follows:

\begin{code}
data Fin : Nat -> Type where
   FZ : Fin (S k)
   FS : Fin k -> Fin (S k)
\end{code}

\noindent
\tDC{FZ} is the zeroth element of a finite set with \texttt{S k} elements; \texttt{FS n} is the \texttt{n+1}th element of a finite set with \texttt{S k} elements.
\tTC{Fin} is indexed by a \tTC{Nat}, which represents the number of elements in the set.
Obviously we can't construct an element of an empty set, so neither constructor targets \texttt{Fin Z}.

A useful application of the \tTC{Fin} family is to represent bounded natural numbers.
Since the first \tTC{n} natural numbers form a finite set of \tTC{n} elements, we can treat \tTC{Fin n} as the set of natural numbers bounded by \tTC{n}.

For example, the following function which looks up an element in a \tTC{Vect}, by a bounded index given as a \tTC{Fin n}, is defined in the prelude:

\begin{code}
index : Fin n -> Vect n a -> a
index FZ     (x :: xs) = x
index (FS k) (x :: xs) = index k xs
\end{code}

\noindent
This function looks up a value at a given location in a vector.
The location is bounded by the length of the vector (\texttt{n} in each case), so there is no
need for a run-time bounds check.
The type checker guarantees that the location is no larger than the length of the vector.

Note also that there is no case for \texttt{Nil} here.
This is because it is impossible. Since there is no element of \texttt{Fin Z}, and the location is a \texttt{Fin n}, then \texttt{n} can not be \tDC{Z}.
As a result, attempting to look up an element in an empty vector would give a compile time type error, since it would force \texttt{n} to be \tDC{Z}.


\subsubsection{Implicit Arguments}

Let us take a closer look at the type of \texttt{index}:

\begin{code}
index : Fin n -> Vect n a -> a
\end{code}

\noindent
It takes two arguments, an element of the finite set of \texttt{n} elements, and a vector with \texttt{n} elements of type \texttt{a}. But there are also two names, \texttt{n} and \texttt{a}, which are not declared  explicitly.
These are \emph{implicit} arguments to \texttt{index}. We could also write the type of \texttt{index} as:

\begin{code}
index : {a:Type} -> {n:Nat} -> Fin n -> Vect n a -> a
\end{code}

\noindent
Implicit arguments, given in braces \texttt{\{\}} in the type declaration, are not given in applications of \texttt{index}; their values can be inferred from the types of the \texttt{Fin n} and \texttt{Vect n a} arguments.
Any name with a \remph{lower case initial letter} which appears as a parameter or index in a type declaration, but which is otherwise free, will be automatically bound as an implicit argument.
Implicit arguments can still be given explicitly in applications, using \texttt{\{a=value\}} and \texttt{\{n=value\}}, for example:

\begin{code}
index {a=Int} {n=2} FZ (2 :: 3 :: Nil)
\end{code}

\noindent
In fact, any argument, implicit or explicit, may be given a name.
We could have declared the type of \texttt{index} as:

\begin{code}
index : (i:Fin n) -> (xs:Vect n a) -> a
\end{code}

\noindent
It is a matter of taste whether you want to do this --- sometimes it can help document a function by making the purpose of an argument more clear.

\subsubsection{``\texttt{using}'' notation}

Sometimes it is useful to provide types of implicit arguments, particularly
where there is a dependency ordering, or where the implicit arguments themselves
have dependencies.
For example, we may wish to state the types of the implicit arguments
in the following definition, which defines a predicate on vectors:

\begin{code}
data Elem : a -> Vect n a -> Type where
   Here :  {x:a} ->   {xs:Vect n a} -> Elem x (x :: xs)
   There : {x,y:a} -> {xs:Vect n a} -> Elem x xs -> Elem x (y :: xs)
\end{code}

\noindent
An instance of \texttt{Elem x xs} states that \texttt{x} is an element of
\texttt{xs}.
We can construct such a predicate if the required element is \texttt{Here}, at the head of the vector, or \texttt{There}, in the tail of the vector.
For example:

\begin{code}
testVec : Vect 4 Int
testVec = 3 :: 4 :: 5 :: 6 :: Nil

inVect : Elem 5 testVec
inVect = There (There Here)
\end{code}

\noindent
If the same implicit arguments are being used a lot, it can make a definition difficult to read.
To avoid this problem, a \texttt{using} block gives the types and ordering of any implicit arguments which can appear within the block:

\begin{code}
using (x:a, y:a, xs:Vect n a)
  data Elem : a -> Vect n a -> Type where
     Here  : Elem x (x :: xs)
     There : Elem x xs -> Elem x (y :: xs)
\end{code}

\subsubsection*{Note: Declaration Order and \texttt{mutual} blocks}

In general, functions and data types must be defined before use, since dependent types allow functions to appear as part of types, and their reduction behaviour to affect type checking.
However, this restriction can be relaxed by using a \texttt{mutual} block, which allows data types and functions to be defined simultaneously:

\begin{code}
mutual
  even : Nat -> Bool
  even Z = True
  even (S k) = odd k

  odd : Nat -> Bool
  odd Z = False
  odd (S k) = even k
\end{code}


\noindent
In a \texttt{mutual} block, first all of the type declarations are added, then the function bodies.
As a result, none of the function types can depend on the reduction behaviour of any of the functions in the block.

\subsection{I/O}

Computer programs are of little use if they do not interact with the user or the system in some way.
The difficulty in a pure language such as \Idris{} --- that is, a language where expressions do not have side-effects --- is that I/O is inherently side-effecting.
Therefore in \Idris{}, such interactions are encapsulated in the type \texttt{IO}:

\begin{code}
data IO a -- IO operation returning a value of type a
\end{code}

\noindent
We'll leave the definition of \texttt{IO} abstract, but effectively it describes what the I/O operations to be executed are, rather than how to execute them.
The resulting operations are executed externally, by the run-time system.
We've already seen one IO program:

\begin{code}
main : IO ()
main = putStrLn "Hello world"
\end{code}


\noindent
The type of \texttt{putStrLn} explains that it takes a string, and returns an element of the unit type \texttt{()} via an I/O action.
There is a variant \texttt{putStr} which outputs a string without a newline:

\begin{code}
putStrLn : String -> IO ()
putStr   : String -> IO ()
\end{code}

We can also read strings from user input:

\begin{code}
getLine : IO String
\end{code}


\noindent
A number of other I/O operations are defined in the prelude, for example for reading and writing files, including:

\begin{code}
data File -- abstract
data Mode = Read | Write | ReadWrite

openFile  : String -> Mode -> IO File
closeFile : File -> IO ()

fread  : File -> IO String
fwrite : File -> String -> IO ()
feof   : File -> IO Bool

readFile : String -> IO String
\end{code}


\subsection{``\texttt{do}'' notation}
\label{sect:do}

I/O programs will typically need to sequence actions, feeding the output of one computation into the input of the next.
\texttt{IO} is an abstract type, however, so we can't access the result of a computation directly.
Instead, we sequence operations with \texttt{do} notation:

\begin{code}
greet : IO ()
greet = do putStr "What is your name? "
           name <- getLine
           putStrLn ("Hello " ++ name)
\end{code}


\noindent
The syntax \texttt{x <- iovalue} executes the I/O operation \texttt{iovalue}, of type \texttt{IO a}, and puts the result, of type \texttt{a} into the variable \texttt{x}.
In this case, \texttt{getLine} returns an \texttt{IO String}, so \texttt{name} has type \texttt{String}.
Indentation is significant --- each statement in the do block must begin in the same column.
The \texttt{return} operation allows us to inject a value directly into an IO operation:

\begin{code}
return : a -> IO a
\end{code}

\noindent
As we will see later, \texttt{do} notation is more general than this, and can be overloaded.

\subsection{Laziness}

\label{sect:lazy}
Normally, arguments to functions are evaluated before the function itself
(that is, \Idris{} uses \emph{eager} evaluation). However, this is not always
the best approach. Consider the following function:

\begin{code}
boolCase : Bool -> a -> a -> a;
boolCase True  t e = t;
boolCase False t e = e;
\end{code}

\noindent
This function uses one of the \texttt{t} or \texttt{e} arguments, but not both (in fact, this is used to implement the \texttt{if...then...else} construct as we will see later.
We would prefer if \emph{only} the argument which was used was evaluated.
To achieve this, \Idris{} provides a \texttt{Lazy} data type, which allows evaluation to be suspended:

\begin{code}
data Lazy : Type -> Type where
     Delay : (val : a) -> Lazy a

Force : Lazy a -> a
\end{code}

\noindent
A value of type \texttt{Lazy a} is unevaluated until it is forced by \texttt{Force}.
The \Idris{} type checker knows about the \texttt{Lazy} type, and inserts conversions where necessary between \texttt{Lazy a} and \texttt{a}, and vice versa.
We can therefore write \texttt{boolCase} as follows, without any explicit
use of \texttt{Force} or \texttt{Delay}:

\begin{code}
boolCase : Bool -> Lazy a -> Lazy a -> a;
boolCase True  t e = t;
boolCase False t e = e;
\end{code}


\subsection{Useful Data Types}

\Idris{} includes a number of useful data types and library functions (see the \texttt{libs/} directory in the distribution).
This chapter describes a few of these.
The functions described here are imported automatically by every \Idris{} program, as part of \texttt{Prelude.idr}.

\subsubsection{\texttt{List} and \texttt{Vect}}

We have already seen the \texttt{List} and \texttt{Vect} data types:

\begin{code}
data List a = Nil | (::) a (List a)

data Vect : Nat -> Type -> Type where
   Nil  : Vect Z a
   (::) : a -> Vect k a -> Vect (S k) a
\end{code}

\noindent
Note that the constructor names are the same for each --- constructor names (in fact, names in general) can be overloaded, provided that they are declared in different namespaces (see Section \ref{sect:namespaces}), and will typically be resolved according to their type.
As syntactic sugar, any type with the constructor names \texttt{Nil} and \texttt{::} can be written in list form.
For example:

\begin{itemize}
\item \texttt{[]} means \texttt{Nil}
\item \texttt{[1,2,3]} means \texttt{1 :: 2 :: 3 :: Nil}
\end{itemize}

\noindent
The library also defines a number of functions for manipulating these types.
\texttt{map} is overloaded both for \texttt{List} and \texttt{Vect} and applies a function to every element of the list or vector.

\begin{code}
map : (a -> b) -> List a -> List b
map f []        = []
map f (x :: xs) = f x :: map f xs

map : (a -> b) -> Vect n a -> Vect n b
map f []        = []
map f (x :: xs) = f x :: map f xs
\end{code}


\noindent
For example, given the following vector of integers, and a function to double an integer:

\inputIdrisListing[firstline=2,lastline=6]{./examples/usefultypes.idr}

\noindent
the function \texttt{map} can be used as follows to double every element in the vector:

\begin{lstlisting}[style=stdout]
*usefultypes> show (map double intVec)
"[2, 4, 6, 8, 10]" : String
\end{lstlisting}

\noindent
You'll find these examples in \texttt{usefultypes.idr} in the \texttt{examples/} directory.
For more details of the functions available on \texttt{List} and \texttt{Vect}, look in the library files:

\begin{itemize}
\item\texttt{libs/prelude/Prelude/List.idr}
\item\texttt{libs/base/Data/List.idr}
\item\texttt{libs/base/Data/Vect.idr}
\item\texttt{libs/base/Data/VectType.idr}
\end{itemize}

\noindent
Functions include filtering, appending, reversing, and so on.
Also remember that \Idris{} is still in development, so if you don't see the function you need, please feel free to add it and submit a patch!

\subsubsection*{Aside: Anonymous functions and operator sections}

There are actually neater ways to write the above expression.
One way would be to use an anonymous function:

\begin{lstlisting}[style=stdout]
*usefultypes> show (map (\x => x * 2) intVec)
"[2, 4, 6, 8, 10]" : String
\end{lstlisting}


\noindent
The notation \lstinline!\x => val! constructs an anonymous function which takes one argument, \texttt{x} and returns the expression \texttt{val}.
Anonymous functions may take several arguments, separated by commas, e.g.\ \lstinline!\x, y, z => val!.
Arguments may also be given explicit types, e.g.\ \lstinline!\x : Int => x * 2!, and can pattern match, e.g.\ \lstinline!\(x, y) => x + y!.
We could also use an operator section:

\begin{lstlisting}[style=stdout]
*usefultypes> show (map (* 2) intVec)
"[2, 4, 6, 8, 10]" : String
\end{lstlisting}


\noindent
\lstinline!(*2)! is shorthand for a function which multiplies a number by 2.
It expands to \lstinline!\x => x * 2!.
Similarly, \texttt{(2*)} would expand to \lstinline!\x => 2 * x!.

\subsubsection{Maybe}

\texttt{Maybe} describes an optional value.
Either there is a value of the given type, or there isn't:

\begin{code}
data Maybe a = Just a | Nothing
\end{code}


\noindent
\texttt{Maybe} is one way of giving a type to an operation that may fail.
For example, looking something up in a \texttt{List} (rather than a vector) may result in an out of bounds error:

\inputIdrisListing[firstline=11, lastline=15]{./examples/usefultypes.idr}

\noindent
The \texttt{maybe} function is used to process values of type \texttt{Maybe}, either by applying a function to the value, if there is one, or by providing a default value:

\begin{code}
maybe : Lazy b -> (a -> b) -> Maybe a -> b
\end{code}

\noindent
Note that the type of the first argument is \texttt{Lazy b} rather than simply \texttt{b}.
Since the default value might not be used, we mark it as \texttt{Lazy} in case it is a large expression where evaluating it then discarding it would be wasteful.

\subsubsection{Tuples and Dependent Pairs}

Values can be paired with the following built-in data type:

\begin{code}
data Pair a b = MkPair a b
\end{code}


\noindent
As syntactic sugar, we can write \texttt{(a, b)} which, according to context, means either \texttt{Pair a b} or \texttt{MkPair a b}.
Tuples can contain an arbitrary number of values, represented as nested pairs:

\begin{code}
fred : (String, Int)
fred = ("Fred", 42)

jim : (String, Int, String)
jim = ("Jim", 25, "Cambridge")
\end{code}


\subsubsection*{Dependent Pairs}

Dependent pairs allow the type of the second element of a pair to depend on the value of the first element.
Traditionally, these are referred to as ``sigma types'':

\begin{code}
data Sigma : (A : Type) -> (P : A -> Type) -> Type where
   MkSigma : {P : A -> Type} -> (a : A) -> P a -> Sigma A P
\end{code}

\noindent
Again, there is syntactic sugar for this. \texttt{(a : A ** P)} is the type of a pair of A and P, where the name \texttt{a} can occur inside \texttt{P}.
\texttt{( a ** p )}  constructs a value of this type. For example, we can pair a number with a  \texttt{Vect} of a particular length.

\begin{code}
vec : (n : Nat ** Vect n Int)
vec = (2 ** [3, 4])
\end{code}

\noindent
If you like, you can write it out the long way, the two are precisely equivalent.

\begin{code}
vec : Sigma Nat (\n => Vect n Int)
vec = MkSigma 2 [3, 4]
\end{code}

The type checker could of course infer the value of the first element from the
length of the vector.
We can write an underscore \texttt{\_} in place of values which we expect the type checker to fill in, so the above definition could also be written as:

\begin{code}
vec : (n : Nat ** Vect n Int)
vec = (_ ** [3, 4])
\end{code}


\noindent
We might also prefer to omit the type of the first element of the pair, since, again, it can be inferred:

\begin{code}
vec : (n ** Vect n Int)
vec = (_ ** [3, 4])
\end{code}

\noindent
One use for dependent pairs is to return values of dependent types where the index is not necessarily known in advance.
For example, if we filter elements out of a \texttt{Vect} according to some predicate, we will not know in advance what the length of the resulting vector will be:

\begin{code}
filter : (a -> Bool) -> Vect n a -> (p ** Vect p a)
\end{code}


\noindent
If the \texttt{Vect} is empty, the result is easy:

\begin{code}
filter p Nil = (_ ** [])
\end{code}


\noindent
In the \texttt{::} case, we need to inspect the result of a recursive call to \texttt{filter} to extract the length and the vector from the result.
To do this, we use \texttt{with} notation, which allows pattern matching on intermediate values:

\begin{code}
filter p (x :: xs) with (filter p xs)
  | ( _ ** xs' ) = if (p x) then ( _ ** x :: xs' ) else ( _ ** xs' )
\end{code}

\noindent
We will see more on \texttt{with} notation later.

\subsection{More Expressions}

\subsubsection*{\texttt{let} bindings}

Intermediate values can be calculated using \texttt{let} bindings:

\inputIdrisListing[firstline=3, lastline=6]{./examples/letbind.idr}

\noindent
We can do simple pattern matching in \texttt{let} bindings too.
For example, we can extract fields from a record as follows, as well as by pattern matching at the top level:

\inputIdrisListing[firstline=7, lastline=12]{./examples/letbind.idr}

\subsubsection*{List comprehensions}
\label{sec:listcomp}

\Idris{} provides \emph{comprehension} notation as a convenient shorthand for building lists.
The general form is:

\begin{lstlisting}[style=stdout]
[ expression | qualifiers ]
\end{lstlisting}

\noindent
This generates the list of values produced by evaluating the \texttt{expression}, according to the conditions given by the comma separated \texttt{qualifiers}.
For example, we can build a list of Pythagorean triples as follows:

\begin{code}
pythag : Int -> List (Int, Int, Int)
pythag n = [ (x, y, z) | z <- [1..n], y <- [1..z], x <- [1..y],
                         x*x + y*y == z*z ]
\end{code}


\noindent
The \texttt{[a..b]} notation is another shorthand which builds a list of numbers between \texttt{a} and \texttt{b}.
Alternatively \texttt{[a,b..c]} builds a list of numbers between \texttt{a} and \texttt{c} with the increment specified by the difference between \texttt{a} and \texttt{b}.
This works for any numeric type, using the \texttt{count} function from the prelude.

\subsubsection*{\texttt{case} expressions}

Another way of inspecting intermediate values of \emph{simple} types
is to use a \texttt{case} expression.
The following function, for example, splits a string into two at a given character:

\inputIdrisListing[firstline=13]{./examples/letbind.idr}

\noindent
\texttt{break} is a library function which breaks a string into a pair of strings
at the point where the given function returns true.
We then deconstruct the pair it returns, and remove the first character of the second string.

A \texttt{case} expression can match several cases, for example, to inspect an intermediate value of type \texttt{Maybe a}.
Recall \texttt{list\_lookup} which looks up an index in a list, returning \texttt{Nothing} if the index is out
of bounds.
We can use this to write \texttt{lookup\_default}, which looks up an index and returns a default value if the index is out of bounds:

\inputIdrisListing[firstline=16]{./examples/usefultypes.idr}

\noindent
If the index is in bounds, we get the value at that index, otherwise we get a default value:

\begin{lstlisting}[style=stdout]
*usefultypes> lookup_default 2 [3,4,5,6] (-1)
5 : Integer
*usefultypes> lookup_default 4 [3,4,5,6] (-1)
-1 : Integer
\end{lstlisting}


\noindent
\textbf{Restrictions:} The \texttt{case} construct is intended for simple analysis of intermediate expressions to avoid the need to write auxiliary functions, and is also used internally to implement pattern matching \texttt{let} and lambda bindings.
It will \emph{only} work if:

\begin{itemize}
\item Each branch \emph{matches} a value of the same type, and \emph{returns} a value of the same type.
\item The type of the result is ``known''. i.e.\ the type of the expression can be determined \emph{without} type checking the \texttt{case}-expression itself.
\end{itemize}

\subsection{Dependent Records}

\emph{Records} are data types which collect several values (the record's \emph{fields}) together.
\Idris{} provides syntax for defining records and automatically generating field access and update functions.
For example, we can represent a person's name and age in a record:

\begin{code}
record Person : Type where
    MkPerson : (name : String) ->
               (age : Int) -> Person

fred : Person
fred = MkPerson "Fred" 30
\end{code}


\noindent
Record declarations are like \texttt{data} declarations, except that they are  introduced by the \texttt{record} keyword, and can only have one constructor.
The names of the binders in the constructor type (\texttt{name} and \texttt{age}) here are the field names, which we can use to access the field values:

\begin{lstlisting}[style=stdout]
*record> name fred
"Fred" : String
*record> age fred
30 : Int
*record> :t name
name : Person -> String
\end{lstlisting}

\noindent
We can also use the field names to update a record (or, more precisely, produce a new record with the given fields updated).

\begin{lstlisting}[style=stdout]
*record> record { name = "Jim" } fred
MkPerson "Jim" 30 : Person
*record> record { name = "Jim", age = 20 } fred
MkPerson "Jim" 20 : Person
\end{lstlisting}

\noindent
The syntax \texttt{record \{ field = val, ... \}} generates a function which updates the given fields in a record.

Records, and fields within records, can have dependent types.
Updates are allowed to change the type of a field, provided that the result is well-typed, and the result does not affect the type of the record as a whole.
For example:

\begin{code}
record Class : Type where
    ClassInfo : (students : Vect n Person) ->
                (className : String) ->
                Class
\end{code}

\noindent
It is safe to update the \texttt{students} field to a vector of a different length because it will not affect the type of the record:

\begin{code}
addStudent : Person -> Class -> Class
addStudent p c = record { students = p :: students c } c
\end{code}

\begin{lstlisting}[style=stdout]
*record> addStudent fred (ClassInfo [] "CS")
ClassInfo (prelude.vect.:: (MkPerson "Fred" 30) (prelude.vect.Nil)) "CS"
  : Class
\end{lstlisting}

\subsubsection*{Nested record update}

\Idris{} also provides a convenient syntax for accessing and updating nested
records. For example, if a field is accessible with the expression
\texttt{c (b (a x))}, it can be updated using the following syntax:

\begin{code}
record { a->b->c = val } x 
\end{code}

\noindent
This returns a new record, with the field accessed by the path 
\texttt{a->b->c} set to \texttt{x}. The syntax is first class, i.e.
\texttt{record \{ a->b->c = val \}} itself has a function type.
Symmetrically, the field can also be accessed with the following syntax:

\begin{code}
record { a->b->c } x
\end{code}


